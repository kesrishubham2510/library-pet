plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.4' apply false
    id 'io.spring.dependency-management' version '1.1.3'
    id 'maven-publish'
    id "org.sonarqube" version "4.4.1.3373"
}

ext.versions = [
        springBootVersion                : '3.1.4',
        springDependencyManagemnetVersion: '1.1.3',
        lombok                           : '1.18.26',
        appVersion                       : '0.0.1',
        group                            : 'com.myreflectionthoughts'
]

jar {
    enabled = true
}

java {
    sourceCompatibility = '17'
}

group = "${versions.group}"
version = "${versions.appVersion}"

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation "org.projectlombok:lombok:${versions.lombok}"
    compileOnly('org.projectlombok:lombok')
    annotationProcessor('org.projectlombok:lombok')
    testCompileOnly('org.projectlombok:lombok')
    testAnnotationProcessor('org.projectlombok:lombok')

}

dependencyManagement {
    imports {
        mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }
}

// to publish the artifact to the nexus-server
publishing {
    publications {
        maven(MavenPublication) {
            group "${versions.group}"
            artifact("build/libs/library-pet-${versions.appVersion}.jar")
        }
    }

    repositories {
        maven {
        // to fetch the properties from the gradle.properties file
        name property('nexusRepoIdentifier')
        url  property('nexusURL')
        allowInsecureProtocol true
        credentials {
            username property('nexusUsername')
            password property('nexusPassword')
      }
    }
  }
}

sonar {
    /*
         * the configuration phase allows access to project delegate
         * accessing the constants from gradle.properties using Project object
     */
    properties {
        property 'sonar.projectKey' , project.property('projectKey')
        property 'sonar.projectName' , project.property('projectName')
        property 'sonar.host.url' , project.property('sonarUrl')
        property 'sonar.token', project.property('projectToken')
    }
}

tasks.named("publishMavenPublicationToNexus-localRepository") {
    dependsOn(tasks.named("jar"))
}

tasks.named("sonarqube"){
    dependsOn(tasks.named("jar"))
}